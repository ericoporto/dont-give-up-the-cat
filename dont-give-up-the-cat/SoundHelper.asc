// new module script
int _previous_rnd;
AudioChannel* _ac_amb;
AudioChannel* _ac_mus;

void Sound_SFXPlay(AudioClip* sfx)
{
  sfx.Play(eAudioPriorityHigh, eOnce);  
}

void PlayRandomSound(int volume, int n, AudioClip* ac_1,  AudioClip* ac_2,  AudioClip* ac_3,  AudioClip* ac_4,  AudioClip* ac_5)
{
  int rnd = Random(n-1);
  AudioChannel* achannel;
  
  while(rnd == _previous_rnd) {
    rnd = Random(n-1);
  }
  
  switch(rnd)
  {
    case 0:
      achannel = ac_1.Play(eAudioPriorityLow, eOnce);
    break;
    case 1:
      achannel = ac_2.Play(eAudioPriorityLow, eOnce);    
    break;
    case 2:
      achannel = ac_3.Play(eAudioPriorityLow, eOnce);    
    break;
    case 3:
      achannel = ac_4.Play(eAudioPriorityLow, eOnce);    
    break;
    case 4:
      achannel = ac_5.Play(eAudioPriorityLow, eOnce);    
    break;
  } 
  _previous_rnd = rnd;
  
  if(achannel != null) achannel.Volume = volume;
}

void Sound_AmbientPlay(AudioClip* amb)
{
  _ac_amb = amb.Play(eAudioPriorityHigh, eRepeat);  
  if( _ac_amb != null) _ac_amb.Volume = 100;
}

void Sound_AmbientSetVolume(int vol)
{
  if( _ac_amb != null && _ac_amb.IsPlaying) _ac_amb.Volume = vol; 
}

void Sound_AmbientStop() 
{
  if(_ac_amb != null && _ac_amb.IsPlaying) _ac_amb.Stop();
}

void Sound_MusicPlay(AudioClip* mus)
{
  _ac_mus = mus.Play(eAudioPriorityHigh, eRepeat);
  if(_ac_mus != null) _ac_mus.Volume = 80;
}

void Sound_MusicFadeOut(float time)
{
  if(_ac_mus != null) _ac_mus.TweenFadeOut(time, eEaseLinearTween, eNoBlockTween);
}

void Sound_MusicStop()
{
  if( _ac_mus != null && _ac_mus.IsPlaying) _ac_mus.Stop();
}

void on_event (EventType event, int data)
{
  switch(event) {
    // called just before the room's 'Player Enters Room' event occurs
    case eEventEnterRoomBeforeFadein: // data = new room number
    break;
    
    // called just after the room's 'Player Leaves Room' event occurs
    case eEventLeaveRoom: //  data = room number they are leaving
      if(_ac_amb != null && _ac_amb.IsPlaying) _ac_amb.Stop();
    break;
  }
}