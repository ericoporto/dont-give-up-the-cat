// new module script

#define SMOKE_FRAMES 8
#define SMOKE_MAX_LIFE 120
#define SMOKE_INIT_HEIGHT 96.0

Mode7World m7_tmp;

int _getSprForLife(int life)
{
  return SPR_SMOKE + ((life*SMOKE_FRAMES)/SMOKE_MAX_LIFE);
}

void SmokeFx::Init(int room_x, int room_y)
{
  this._RoomX = room_x;
  this._RoomY = room_y;
  
  for(int i=0; i<SMOKE_COUNT; i++)
  {
    Mode7Object* m7oSmoke = m7_tmp.AddObject(room_x, room_y, 0.5, SPR_SMOKE);
    int life = Random(SMOKE_MAX_LIFE-1);
    m7oSmoke.Y = SMOKE_INIT_HEIGHT + IntToFloat(life);
    this._Life[i] = life;
    this.M7Object[i] = m7oSmoke;
  }
}

void SmokeFx::Update()
{  
  for(int i=0; i<SMOKE_COUNT; i++)
  {
    Mode7Object* m7oSmoke = this.M7Object[i];
    
    this._Life[i]++;
    
    if(this._Life[i] < SMOKE_MAX_LIFE) {
      m7oSmoke.Graphic = _getSprForLife(this._Life[i]);
      m7oSmoke.X += IntToFloat(Random(4)-2)/2.0;
      m7oSmoke.Z += IntToFloat(Random(4)-2)/2.0;
      m7oSmoke.Y += 1.0;
    } else {   
      // reset
      this._Life[i] = 0;
      m7oSmoke.X = RoomToWorldX(this._RoomX);
      m7oSmoke.Z = RoomToWorldZ(this._RoomY);
      m7oSmoke.Y = SMOKE_INIT_HEIGHT;
      m7oSmoke.Graphic = _getSprForLife(this._Life[i]);  
    }
  }  
}