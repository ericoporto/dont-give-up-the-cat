// room script file
DynamicSprite* ground;
ZoneManager zManager;
Mode7World m7;

Mode7Object* o7home;
Mode7Object* o7player;
Mode7Object* o7grave_house;
Mode7Object* o7grave_flowers;
Mode7Object* o7grave_stones;

Mode7Object* o7ghost_floatingcat;
Mode7Object* o7ghost_forgotten;
Mode7Object* o7ghost_standingcat;

InputParams* hidPlayer;

void update_m7wobjs_from_zmanager()
{
  m7.RemoveAllsObjects();
  
 // int count = 0;
  
  zManager.ResetObjectActiveZones();
  Mode7Object* obj = zManager.GetNextObjectActiveZones();
  while(obj != null)
  {
    m7.AddExternalObject(obj);
    //count++;
    
    obj = zManager.GetNextObjectActiveZones();
  }
  //Display("Updated %d objects", count);
}

void begin_game(int tree_count)
{
  o7ghost_floatingcat = null;
  o7ghost_forgotten = null;

  zManager.WorldWidth = Room.Width;
  zManager.WorldHeight = Room.Height;
  zManager.ZoneXYCount = 8;
  int half_world_width = Room.Width/2;
  int half_world_height = Room.Height/2;
    
  hidPlayer = InputParams.NewInputParams(1.4, 4.0, 4.0, 2.0, 2.0);
  
  int init_x = 886 -half_world_width;
  int init_y = 734 -half_world_height;
  o7player = zManager.AddObject(init_x, init_y, 0.36, 2);
  o7player.Visible = false;
  o7player.Angle = Maths.Pi * 0.25;
  
  int home_init_x = 880 -half_world_width;
  int home_init_y = 658 -half_world_height;   
  o7home = zManager.AddObject(home_init_x, home_init_y, 0.5, 27);
    
  init_x = 1140 -half_world_width;
  init_y = 1106 -half_world_height;
  o7grave_house = zManager.AddObject(init_x, init_y, 0.36, SPR_GRAVE_HOUSE);
  
  init_x = 1509 -half_world_width;
  init_y = 470 -half_world_height;
  o7grave_flowers = zManager.AddObject(init_x, init_y, 0.5, SPR_GRAVE_FLOWERS);
  
  init_x = 457 -half_world_width;
  init_y = 1277 -half_world_height;
  o7grave_stones = zManager.AddObject(init_x, init_y, 0.5, SPR_GRAVE_STONES);
    
  PositionGenerator pgen;
  pgen.Init(Room.Width, Room.Height, 8);
  
  for(int i=0; i<tree_count; i++)
  {
    int obj_x, obj_y;
    TerrainType obj_terrain;
    do {
      Point* prand = pgen.Generate();
      
      obj_x = prand.x - half_world_width;
      obj_y = prand.y - half_world_height;
      obj_terrain = GetTerrainAtRoomXY(obj_x+half_world_width, obj_y+half_world_height);
    } while (obj_terrain == eTT_Water || obj_terrain == eTT_Clearance);
    
    int gfx;
    if(obj_terrain == eTT_Land) {
      gfx = SPR_GRASS_LOW;
    } else if(obj_terrain == eTT_LandBorder) {
      gfx = SPR_GRASS_HIGH;      
    } else if(obj_terrain == eTT_Tree) {
      int rnd = Random(15);
      gfx = SPR_GRASS_HIGH;
      if(rnd == 4) gfx = SPR_TREE_SMALL;
      if(rnd == 5) gfx = SPR_TREE_BIG;
    } else if(obj_terrain == eTT_Flowers) {
      int rnd = Random(2);
      gfx = SPR_FLOWER_0 + rnd;
    } else if(obj_terrain == eTT_FlowersBlue) {
      int rnd = Random(1);
      gfx = SPR_FLOWER_BLUE_0 + rnd;
    } else if(obj_terrain == eTT_BlueRock) {
      int rnd = Random(2);
      gfx = SPR_BLUEROCK_1;
      if(rnd == 1) gfx = SPR_BLUEROCK_0;
    } else if(obj_terrain == eTT_GrassyRock) {
      int rnd = Random(3);
      gfx = SPR_GRASSYROCK;
      if(rnd == 3) gfx = SPR_GRASS_HIGH;      
    } else {
      int rnd = Random(2);
      gfx = SPR_GRASS_HIGH;
      if(rnd == 2) gfx = SPR_TREE_SMALL;
      if(rnd == 1) gfx = SPR_TREE_BIG;       
    } 
    
    Mode7Object* m7obj = zManager.AddObject(obj_x, obj_y, 1.0, gfx);
    
    if(gfx == 14) m7obj.Y = -4.0;
  }
  
  update_m7wobjs_from_zmanager();
  
  m7.SetViewscreen(VIEW_SCREEN_WIDTH, VIEW_SCREEN_HEIGHT);
  // m7.SetBgColor(19544); // water
  m7.SetBgColor(BG_COLOR);
  m7.SetSkyColor(BG_COLOR);
 // m7.SetSkyColor(12716);
  create_depth_fog(BG_COLOR, 10000);
    
  DrawingSurface* surf = Room.GetDrawingSurfaceForBackground();
  ground = DynamicSprite.CreateFromDrawingSurface(surf, 0, 0, Room.Width, Room.Height);
  surf.Release();
  
  m7.SetGroundSprite(ground.Graphic);
  m7.SetHorizonSprite(SPR_HORIZON);
  skybox_create(BG_COLOR, SPR_HORIZON);
  m7.SetCamera(o7player.X, o7player.Y + 6.0, o7player.Z, 0.0, 0.0, 175.0);
}

// room events
function room_Load()
{
  //TintScreen(55, 25, 60);
  begin_game(/* obj count*/ 4020);
}

function room_RepExec()
{
  if (IsGamePaused()) { 
    if(!mouse.Visible) mouse.Visible = true;
    return;
  }
  if(mouse.Visible) mouse.Visible = false;
  
  input_player(o7player, hidPlayer);
  
  m7.CameraAngleX = hidPlayer.AngleX;
  
  m7.TargetCamera(o7player.X, o7player.Y, o7player.Z, o7player.Angle, eCameraTarget_FirstPerson);
  zManager.UpdateCurrentPos(o7player.X, o7player.Z);
  
  int x = FloatToInt(o7player.X)+Room.Width/2;
  int y = FloatToInt(o7player.Z)+Room.Height/2;
  
  if(zManager.ZoneChanged)
  {
   // aSfx_coin_single4.Play();
    update_m7wobjs_from_zmanager();
  }
  
  m7.UpdateObjects(false);
  
  // determine rotating objects sprite
  if(o7home.Visible) {
    int home_sprite = m7.GetAngleObjectAndCamera(o7home)/2;
    if (home_sprite <= 0) {
      home_sprite = ClampI(SPR_HOME_ANGL_000 - home_sprite, SPR_HOME_ANGL_000, SPR_HOME_ANGL_358);
    } else {
      home_sprite = ClampI(SPR_HOME_ANGL_358 - home_sprite, SPR_HOME_ANGL_000, SPR_HOME_ANGL_358);
    }
    o7home.Graphic = home_sprite; 
  }
  
  if(o7ghost_floatingcat != null && o7ghost_floatingcat.Visible) {
    o7ghost_floatingcat.Y = 26.0 + 8.0*Maths.Sin(Maths.Pi * IntToFloat( Frame % 64)/ 32.0);
  }  
  if(o7ghost_forgotten != null && o7ghost_forgotten.Visible) {
    o7ghost_forgotten.Y = 24.0 + 8.0*Maths.Sin(Maths.Pi * IntToFloat( Frame % 64)/ 32.0);
  }
  if(o7ghost_standingcat != null && o7ghost_standingcat.Visible) {
    o7ghost_standingcat.Y = 8.0 + 4.0*Maths.Sin(Maths.Pi * IntToFloat( Frame % 64)/ 32.0);
  }
  
  m7.Draw();
  m7.DrawObjectsOverlay();
}

function on_mouse_click(MouseButton button)
{
  if (IsGamePaused()) {
    // game is paused, so do nothing (i.e. don't process mouse clicks)
  } else if (button == eMouseLeft) {
    if(do_claw())
    {
      float sin = Maths.Sin(Maths.DegreesToRadians(o7player.Angle));
      float cos = Maths.Cos(Maths.DegreesToRadians(o7player.Angle));
      float x = o7player.X + 8.0 * sin;
      float z = o7player.Z - 8.0 * cos;
      
      Mode7Object* obj = m7.GetObject(x, z, 32.0, CLAWABLE_GRAPHICS_COUNT, clawable_graphics);
      obj = clawn_object(obj);
      if(obj == null) return;
      if(obj == o7grave_flowers && o7ghost_floatingcat == null) {
        o7ghost_floatingcat = zManager.AddObject(
          FloatToInt(o7grave_flowers.X)+1, FloatToInt(o7grave_flowers.Z)+1, 0.36, SPR_GHOST_FLOATINGCAT
          );
        update_m7wobjs_from_zmanager();
      }
      if(obj == o7grave_house && o7ghost_forgotten == null) {
        o7ghost_forgotten = zManager.AddObject(
          FloatToInt(o7grave_house.X)-2, FloatToInt(o7grave_house.Z)-2, 0.36, SPR_GHOST_FORGOTTEN
          );
        update_m7wobjs_from_zmanager();          
      }
      if(obj == o7grave_stones && o7ghost_standingcat == null) {
        o7ghost_standingcat = zManager.AddObject(
          FloatToInt(o7grave_stones.X)+1, FloatToInt(o7grave_stones.Z)+2, 0.36, SPR_GHOST_STANDINGCAT
          );
        update_m7wobjs_from_zmanager();          
      }
    }
  }
}

void on_key_press(eKeyCode k, int mod) {
  //m7.DebugKeyPress(k); 
}

// Clouds: ansimuz - Sunnyland