// room script file
DynamicSprite* ground;
ZoneManager zManager;
Mode7World m7;
ScreenEffects screen_fx;

Mode7Object* o7home;
Mode7Object* o7player;
Mode7Object* o7grave_house;
Mode7Object* o7grave_flowers;
Mode7Object* o7grave_stones;

Mode7Object* o7ghost_floatingcat;
Mode7Object* o7ghost_forgotten;
Mode7Object* o7ghost_standingcat;

InputParams* hidPlayer;

void update_m7wobjs_from_zmanager()
{
  m7.RemoveAllsObjects();
  
  zManager.ResetObjectActiveZones();
  Mode7Object* obj = zManager.GetNextObjectActiveZones();
  while(obj != null)
  {
    m7.AddExternalObject(obj);
    
    obj = zManager.GetNextObjectActiveZones();
  }
}

void aproximate_o7_to_char_position(Character* c, Mode7Object* o7c, float lerp_factor)
{
  if(WorldToRoomX(o7c.X) != c.x && WorldToRoomY(o7c.Y) != c.y) {
    o7c.X = Lerp(o7c.X, RoomToWorldX(c.x), lerp_factor);
    o7c.Z = Lerp(o7c.Z, RoomToWorldZ(c.y), lerp_factor);
  }
}

void update_ghost_position(Character* cGhost, Mode7Object* o7Ghost, float medium_heigh, float float_range, int follow_dist, int follow_eagerness, TerrainType player_terrain)
{
  if(o7Ghost != null) {
    aproximate_o7_to_char_position(cGhost, o7Ghost, 0.2);
    if(o7Ghost.Visible) {
      o7Ghost.Y = medium_heigh + float_range*Maths.Sin(Maths.Pi * IntToFloat( Frame % 64)/ 32.0);
      if(player_terrain == eTT_ClearanceHome && cGhost.GetFollowingCharacter() != cHome) {
        if(GetDistance(player.x, player.y, cGhost.x, cGhost.y) <= 48) {
          cGhost.DoFollow(cHome, follow_dist, follow_eagerness);
        }
      }
    }
  }
}

void begin_game(int tree_count)
{
  o7ghost_floatingcat = null;
  o7ghost_forgotten = null;

  screen_fx.Init();
  screen_fx.On = false;

  zManager.WorldWidth = Room.Width;
  zManager.WorldHeight = Room.Height;
  zManager.ZoneXYCount = 8;
    
  hidPlayer = InputParams.NewInputParams(1.4, 4.0, 4.0, 2.0, 2.0);
  
  //o7player = zManager.AddObject(r2wX(886), r2wZ(734), 0.36, 2);
  o7player = zManager.AddObject(r2wX(player.x), r2wZ(player.y), 0.36, 2);
  o7player.Visible = false;
  o7player.Angle = Maths.Pi * 0.25;
  
  o7home = zManager.AddObject(r2wX(880), r2wZ(658), 0.5, 27);
    
  o7grave_flowers = zManager.AddObject(r2wX(1509), r2wZ(470), 0.5, SPR_GRAVE_FLOWERS);
  o7grave_house = zManager.AddObject(r2wX(1140), r2wZ(1106), 0.36, SPR_GRAVE_HOUSE);
  o7grave_stones = zManager.AddObject(r2wX(457), r2wZ(1277), 0.5, SPR_GRAVE_STONES);
    
  PositionGenerator pgen;
  pgen.Init(Room.Width, Room.Height, 8);
  
  for(int i=0; i<tree_count; i++)
  {
    int obj_x, obj_y;
    TerrainType obj_terrain;
    do {
      Point* prand = pgen.Generate();
      obj_x = r2wX(prand.x);
      obj_y = r2wZ(prand.y);
      
      obj_terrain = GetTerrainAtRoomXY(w2rX(obj_x), w2rY(obj_y));
    } while (IsTerrainEmpty(obj_terrain));
    
    Mode7Object* m7obj = zManager.AddObject(obj_x, obj_y, 1.0, SpriteFromTerrainType(obj_terrain));
    if(m7obj.Graphic == SPR_GRASS_HIGH) m7obj.Y = -4.0;
  }
  
  update_m7wobjs_from_zmanager();
  
  m7.SetViewscreen(VIEW_SCREEN_WIDTH, VIEW_SCREEN_HEIGHT);
  m7.SetBgColor(BG_COLOR);
  m7.SetSkyColor(BG_COLOR);
  create_depth_fog(BG_COLOR, 10000);
  
  m7.SetHorizonSprite(SPR_HORIZON);
  skybox_create(BG_COLOR, SPR_HORIZON);
  m7.SetCamera(o7player.X, o7player.Y + 6.0, o7player.Z, 0.0, 0.0, 175.0);
}

// room events
function room_Load()
{
  begin_game(/* obj count*/ 4020);
}

function room_RepExec()
{
  if (IsGamePaused()) { 
    if(!mouse.Visible) mouse.Visible = true;
    return;
  }
  if(mouse.Visible) mouse.Visible = false;
  
  input_player(o7player, hidPlayer);
  
  m7.CameraAngleX = hidPlayer.AngleX;  
  m7.TargetCamera(o7player.X, o7player.Y, o7player.Z, o7player.Angle, eCameraTarget_FirstPerson);
  
  zManager.UpdateCurrentPos(o7player.X, o7player.Z);
  player.x = WorldToRoomX(o7player.X);
  player.y = WorldToRoomY(o7player.Z);
  TerrainType player_terrain = GetTerrainAtRoomXY(player.x, player.y);
  screen_fx.On = ((player_terrain == eTT_ClearanceGFlowers && o7ghost_floatingcat == null) ||
    (player_terrain == eTT_ClearanceGHouse && o7ghost_forgotten == null) ||
    (player_terrain == eTT_ClearanceGStones && o7ghost_standingcat == null));
  
  if(zManager.ZoneChanged)
  {
    update_m7wobjs_from_zmanager();
  }  
  m7.UpdateObjects(false);
  
  // determine rotating objects sprite
  if(o7home.Visible) {
    o7home.Graphic = GetGraphicFromAngle(m7.GetAngleObjectAndCamera(o7home), 2, SPR_HOME_ANGL_000, SPR_HOME_ANGL_358);
  }
  
  // animate and position ghosts as needed
  update_ghost_position(cGhostFloatingcat, o7ghost_floatingcat, 24.0, 8.0, 5, 8, player_terrain);
  update_ghost_position(cGhostForgotten, o7ghost_forgotten, 22.0, 8.0, 5, 50, player_terrain);
  update_ghost_position(cGhostStandingcat, o7ghost_standingcat, 8.0, 4.0, 5, 40, player_terrain);
  
  m7.Draw();
  m7.DrawObjectsOverlay();
  screen_fx.Update();
}

function on_mouse_click(MouseButton button)
{
  if (IsGamePaused()) {
    // game is paused, so do nothing (i.e. don't process mouse clicks)
  } else if (button == eMouseLeft) {
    if(do_claw())
    {
      float sin = Maths.Sin(Maths.DegreesToRadians(o7player.Angle));
      float cos = Maths.Cos(Maths.DegreesToRadians(o7player.Angle));
      float x = o7player.X + 8.0 * sin;
      float z = o7player.Z - 8.0 * cos;
      
      Mode7Object* obj = m7.GetObject(x, z, 32.0, CLAWABLE_GRAPHICS_COUNT, clawable_graphics);
      obj = clawn_object(obj);
      if(obj == null) return;
      if(obj == o7grave_flowers && o7ghost_floatingcat == null) {
        o7ghost_floatingcat = zManager.AddObject(
          FloatToInt(o7grave_flowers.X)+1, FloatToInt(o7grave_flowers.Z)+1, 0.36, SPR_GHOST_FLOATINGCAT
          );
        cGhostFloatingcat.ChangeRoom(player.Room, WorldToRoomX(o7ghost_floatingcat.X), WorldToRoomY(o7ghost_floatingcat.Z));
        cGhostFloatingcat.DoFollow(player, 5, 60);
        update_m7wobjs_from_zmanager();
      }
      if(obj == o7grave_house && o7ghost_forgotten == null) {
        o7ghost_forgotten = zManager.AddObject(
          FloatToInt(o7grave_house.X)-2, FloatToInt(o7grave_house.Z)-2, 0.36, SPR_GHOST_FORGOTTEN
          );
        cGhostForgotten.ChangeRoom(player.Room, WorldToRoomX(o7ghost_forgotten.X), WorldToRoomY(o7ghost_forgotten.Z));
        cGhostForgotten.DoFollow(player, 5, 60);
        update_m7wobjs_from_zmanager();          
      }
      if(obj == o7grave_stones && o7ghost_standingcat == null) {
        o7ghost_standingcat = zManager.AddObject(
          FloatToInt(o7grave_stones.X)+1, FloatToInt(o7grave_stones.Z)+2, 0.36, SPR_GHOST_STANDINGCAT
          );
        cGhostStandingcat.ChangeRoom(player.Room, WorldToRoomX(o7ghost_standingcat.X), WorldToRoomY(o7ghost_standingcat.Z));
        cGhostStandingcat.DoFollow(player, 5, 60);
        update_m7wobjs_from_zmanager();          
      }
    }
  }
}

void on_key_press(eKeyCode k, int mod) {
  //m7.DebugKeyPress(k); 
}

void late_repeatedly_execute_always()
{
  screen_fx.LateUpdate();
}