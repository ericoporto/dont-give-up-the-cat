// room script file
#define MAX_OBJS_AT_END 1280
#define HOME_POS_X 880
#define HOME_POS_Y 658
Mode7World m7;
SmokeFx home_smoke;
Mode7Object* o7home;
Mode7Object* o7grave_house;
Mode7Object* o7cam;
float cam_angle_x;
int fram;

void populate_ending(int pre_created_objs)
{
  bool avoid_center = pre_created_objs != 0;
  int total_objs = MAX_OBJS_AT_END - pre_created_objs;
  
  PositionGenerator pgen;
  pgen.Init(Room.Width, Room.Height, 8);
  
  for(int i=0; i<total_objs; i++)
  {
    int obj_x, obj_y;
    TerrainType obj_terrain;
    do {
      Point* prand = pgen.Generate();
      obj_x = r2wX(prand.x);
      obj_y = r2wZ(prand.y);
      
      obj_terrain = GetTerrainAtRoomXY(w2rX(obj_x), w2rY(obj_y));
      if(avoid_center && i < 800) {
        if(obj_x> 256 && obj_x < 1024 && obj_y > 256 && obj_y < 1024) obj_terrain = eTT_Water;
      }
      
    } while (IsTerrainEmpty(obj_terrain));
    
    Mode7Object* m7obj = m7.AddObject(obj_x + 256, obj_y, 1.0, SpriteFromTerrainType(obj_terrain));
    if(m7obj.Graphic == SPR_GRASS_HIGH) m7obj.Y = -4.0;
  }
    
  o7home = m7.AddObject(r2wX(HOME_POS_X), r2wZ(HOME_POS_Y), 1.5, SPR_HOME_ANGL_000);
  o7home.Y += 1.0;
  home_smoke.Init(HOME_POS_X, HOME_POS_Y, 88.0, 48);

  o7grave_house = m7.AddObject(r2wX(1093), r2wZ(1005), 0.42, SPR_GRAVE_HOUSE);
  
  o7cam = m7.AddObject(r2wX(HOME_POS_X), r2wZ(HOME_POS_Y+70), 0.5, 0);
  o7cam.Y += 1.0;
}

function room_Load()
{
  gEndingDark.Visible = true;
  gEndingDark.Transparency = 100;
  Sound_MusicPlay(aMusic_ending);
  gScreen.Visible = true;
  CustomTransition_IntantFadeOut();
  Sound_AmbientStop();
  Sound_CricketsStop();
  Sound_WindStop();
  
  for(int i=0; i<hacks.ObjectCount; i++) {
    Mode7Object* obj = hacks.M7Object[i];
    
    if(obj == null) continue;
    obj.X -= 256.0;
    
    int gfx = obj.Graphic;
    
    if(gfx >= SPR_HOME_ANGL_000 && gfx <= SPR_HOME_ANGL_358) obj = null;  
    if(gfx >= SPR_SMOKE_0 && gfx <= SPR_SMOKE_7) obj = null;
    if(gfx >= SPR_DANDELION_PARTICLE_0 && gfx <= SPR_DANDELION_PARTICLE_4) obj = null;
    if(gfx == SPR_GHOST_FLOATINGCAT || gfx == SPR_GHOST_FORGOTTEN || gfx == SPR_GHOST_STANDINGCAT) obj = null;
    if(gfx == SPR_GRAVE_HOUSE) obj = null;
    
    if(obj != null) {
      m7.AddExternalObject(obj);
    }
  }
  
  populate_ending(hacks.ObjectCount);
  
  hacks.ReleaseAllObjects();
  
  for(int i=0; i<home_smoke.SmokeCount; i++) {
    m7.AddExternalObject(home_smoke.M7Object[i]);
  }
    
  m7.SetViewscreen(VIEW_SCREEN_WIDTH, VIEW_SCREEN_HEIGHT);
  m7.SetBgColor(BG_COLOR);
  m7.SetSkyColor(BG_COLOR);
  create_depth_fog(BG_COLOR, 10000, 90);
  
  m7.SetHorizonSprite(SPR_HORIZON);
  skybox_create(BG_COLOR, SPR_HORIZON);
  m7.SetCamera(o7cam.X, o7cam.Y + 6.0, o7cam.Z, 0.0, 0.0, 175.0);
}

float radius;
void camera_move_update() {
  if(fram == 0) radius = 0.0;
  fram++;
  int max_fram = 2048;
  float ref_angle = NormalizeAngleRadians(Maths.Pi * IntToFloat(fram % max_fram)/IntToFloat(max_fram/2));
  float ref_cos = Maths.Cos(ref_angle);
  float ref_sin = Maths.Sin(ref_angle);
  float max_radius = 256.0;
  
  if(radius < max_radius/2.0) radius += 0.25;
  else if(radius < max_radius/1.5) radius += 0.125;
  else if(radius < max_radius) radius += 0.0625;
  else radius = max_radius;
  
  float circle_x = radius * ref_cos + o7home.X;
  float circle_y = radius * ref_sin + o7home.Z;
  
  o7cam.X = circle_x; // Lerp(o7cam.X, circle_x, 0.05);
  o7cam.Z = circle_y; //Lerp(o7cam.Z, circle_y, 0.05);
  if(o7cam.Y < 32.0) o7cam.Y += 0.25;
  
  float dx = o7cam.X - o7home.X;
  float dz = o7cam.Z - o7home.Z;
  float dy = o7cam.Y - o7home.Y;
  
  float length = Maths.Sqrt(dx*dx + dz*dz + dy*dy);
  
  float norm_dx = dx / length;
  float norm_dz = dz / length;
  float norm_dy = dy / length;
  
  cam_angle_x = Maths.RadiansToDegrees(Maths.ArcSin(norm_dy));
  o7cam.Angle = Maths.RadiansToDegrees(NormalizeAngleRadians(ref_angle-Maths.Pi/2.0));
}

void update_game_always()
{
  camera_move_update();
  m7.CameraAngleX = cam_angle_x;
  m7.TargetCamera(o7cam.X, o7cam.Y, o7cam.Z, o7cam.Angle, eCameraTarget_FirstPerson);  
  if(Frame_Odd) return;  
  
  // determine rotating objects sprite
  o7home.Graphic = GetGraphicFromAngle(m7.GetAngleObjectAndCamera(o7home), 2, SPR_HOME_ANGL_000, SPR_HOME_ANGL_358);
    
  home_smoke.Update();
  m7.UpdateAndDrawObjects();  
  
  // skybox stuff
  m7.Draw();
}

void repeatedly_execute_always()
{
  update_game_always();
}

function room_RepExec()
{

}

void on_key_press(eKeyCode k, int mod) {
  m7.DebugKeyPress(k); 
}


function room_AfterFadeIn()
{
  gEndingDark.TweenTransparency(5.0, 64, eEaseLinearTween, eNoBlockTween);
  CustomTransition_FadeIn(5.0, eBlockTween);
  credits_do_calls();
  gEndingDark.TweenTransparency(7.0, 0, eEaseLinearTween, eBlockTween);
  gEndingDark.TweenBackgroundColor(2.0, eAgsColor_Black, eEaseLinearTween, eBlockTween);
  gEndingDark.BorderColor = gEndingDark.BackgroundColor;
  credits_do_rolling();
  Sound_MusicFadeOut(4.0);
  Wait(SecondsToLoops(4.0));
  RestartGame();
}

function room_Leave()
{
  gEndingDark.Visible = false;
}
