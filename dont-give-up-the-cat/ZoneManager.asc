// new module script

#define ZONE_FROM_X (FloatToInt(x) + this.WorldWidth/2) / ((this.WorldWidth/this.ZoneXYCount) + 1)
#define ZONE_FROM_Z (FloatToInt(z) + this.WorldHeight/2)  / ((this.WorldHeight/this.ZoneXYCount) + 1)

int ZoneManager::GetZoneID_FromObjPos(float x, float z)
{
  int z_x = ZONE_FROM_X;
  int z_y = ZONE_FROM_Z;
  
  return z_x * this.ZoneXYCount + z_y;
}

protected void ZoneManager::_AddObjectToZone(Mode7Object* m7obj, int obj_id)
{
  int zone_id = this.GetZoneID_FromObjPos(m7obj.X, m7obj.Z);
  
  int zone_real_id = zone_id * MAX_OBJ_PER_ZONE + this.ZoneObjectCount[zone_id];
  
  this.ZoneObjectCount[zone_id]++;
  this.ZoneObject[zone_real_id] = obj_id ;
}

void ZoneManager::RefreshObjectZones()
{
  for(int i=0; i<MAX_ZONES; i++) 
  {
    this.ZoneObjectCount[i] = 0;
  }
  
  for(int i=0; i<this.ObjectCount; i++)
  {
    this._AddObjectToZone(this.Objects[i], i);    
  }
}

protected void ZoneManager::_AdjustVicinityZones()
{
  int min_zn_x,  max_zn_x, min_zn_y, max_zn_y;  
  int mid_x, mid_y;
  
  mid_x = this.CurrentX;
  mid_y = this.CurrentY;
  
  if(mid_x < 1) mid_x = 1;
  if(mid_x > this.ZoneXYCount - 2) mid_x = this.ZoneXYCount - 2;
  
  if(mid_y < 1) mid_y = 1;
  if(mid_y > this.ZoneXYCount - 2) mid_y = this.ZoneXYCount - 2;
  
  min_zn_x = mid_x - 2;
  max_zn_x = mid_x + 2;
  min_zn_y = mid_y - 2;
  max_zn_y = mid_y + 2;
  
  bool min_zn_x_out, max_zn_x_out, min_zn_y_out, max_zn_y_out;
  min_zn_x_out = min_zn_x < 0;
  max_zn_x_out = max_zn_x > this.ZoneXYCount - 1;
  min_zn_y_out = min_zn_y < 0;
  max_zn_y_out = max_zn_y > this.ZoneXYCount - 1;
  
  if(min_zn_x_out) min_zn_x = 0;
  if(max_zn_x_out) max_zn_x = this.ZoneXYCount - 1;
  if(min_zn_y_out) min_zn_y = 0;
  if(max_zn_y_out) max_zn_y = this.ZoneXYCount - 1;  
  
  int vZoneCount = 0;
  
  if(!min_zn_y_out) {
    for(int i=min_zn_x; i<=max_zn_x; i++)
    {
      this._VicinityZones[vZoneCount] = i*this.ZoneXYCount + min_zn_y;
      vZoneCount++;
    }
  }
  
  if(!max_zn_y_out) {
    for(int i=min_zn_x; i<=max_zn_x; i++)
    {
      this._VicinityZones[vZoneCount] = i*this.ZoneXYCount + max_zn_y;
      vZoneCount++;
    }
  }
  
  if(!min_zn_x_out) {
    for(int j=min_zn_y+1; j<=max_zn_y-1; j++)
    {
      this._VicinityZones[vZoneCount] = min_zn_x*this.ZoneXYCount + j;
      vZoneCount++;      
    }    
  }
  
  if(!max_zn_x_out) {
    for(int j=min_zn_y+1; j<=max_zn_y-1; j++)
    {
      this._VicinityZones[vZoneCount] = max_zn_x*this.ZoneXYCount + j;
      vZoneCount++;      
    }    
  }
  
  //lblDebug.Text = "VicZones: ";
  
  //for(int i=0; i<vZoneCount; i++) {
  //  lblDebug.Text = lblDebug.Text.Append(String.Format("%d, ",  this._VicinityZones[i]));
  //}
  
  this._VicinityZoneCount = vZoneCount;
}

protected void ZoneManager::_AdjustActiveZones()
{ 
  int min_zn_x,  max_zn_x, min_zn_y, max_zn_y;  
  int mid_x, mid_y;
  
  mid_x = this.CurrentX;
  mid_y = this.CurrentY;
  
  if(mid_x < 1) mid_x = 1;
  if(mid_x > this.ZoneXYCount - 2) mid_x = this.ZoneXYCount - 2;
  
  if(mid_y < 1) mid_y = 1;
  if(mid_y > this.ZoneXYCount - 2) mid_y = this.ZoneXYCount - 2;  
  
  min_zn_x = mid_x - 1;
  max_zn_x = mid_x + 1; //inclusive
  min_zn_y = mid_y - 1;
  max_zn_y = mid_y + 1; //inclusive
  
  int k=0;
  for(int i=min_zn_x; i<=max_zn_x; i++)
  {
    for(int j=min_zn_y; j<=max_zn_y; j++)
    {
      this._ActiveZones[k] = i*this.ZoneXYCount + j;
      
      k++;
    }
  }
}

int zCCount;
void ZoneManager::UpdateCurrentPos(float x, float z, bool skip_cooldown)
{
  this.ZoneChanged = false;
  this.PreviousX = this.CurrentX;
  this.PreviousY = this.CurrentY;
  
  int currentX = ZONE_FROM_X;
  int currentY = ZONE_FROM_Z;
  
  bool zoneChanged = currentX != this.PreviousX || currentY != this.PreviousY;
  if(zoneChanged) {
    zCCount++;
    if(zCCount>10) {
      this.ZoneChanged = true;
      //Display("Refreshing");
      zCCount = 0;
      if(this._HACK_UpdateRefreshCooldown == 0  || skip_cooldown) {
        this.RefreshObjectZones();
      }
      
      this.CurrentX = currentX;
      this.CurrentY = currentY;
      this._AdjustActiveZones();
      //this._AdjustVicinityZones();
      this._HACK_UpdateRefreshCooldown++;
      if(this._HACK_UpdateRefreshCooldown > 3) {
        this._HACK_UpdateRefreshCooldown = 0;
      }
    }   
    
  }
}


Mode7Object* ZoneManager::AddObject(int x, int z, float factor, int graphic)
{
  if (this.ObjectCount == ZM_MAX_WORLD_OBJ) return null;
  this.Objects[this.ObjectCount] = new Mode7Object;
  this.Objects[this.ObjectCount].Visible = true;
  this.Objects[this.ObjectCount].X = IntToFloat(x);
  this.Objects[this.ObjectCount].Y = 0.0;
  this.Objects[this.ObjectCount].Z = IntToFloat(z);
  this.Objects[this.ObjectCount].Factor = factor;
  this.Objects[this.ObjectCount].Graphic = graphic;
  
  this._AddObjectToZone(this.Objects[this.ObjectCount], this.ObjectCount);
  
  this.ObjectCount++;
  return this.Objects[this.ObjectCount-1];
}

void ZoneManager::ResetObjectActiveZones()
{
  this._iZone = 0;
  this._iZoneObject = 0;
}

Mode7Object* ZoneManager::GetNextObjectActiveZones()
{
  if(this._iZone >= MAX_ACTIVE_ZONES) return null;
  
  int zone_id = this._ActiveZones[this._iZone];
  
  if(this._iZoneObject > this.ZoneObjectCount[zone_id])
  {
    this._iZone++;
    this._iZoneObject = 0;
  }
  
  if(this._iZone >= MAX_ACTIVE_ZONES) return null;
  zone_id = this._ActiveZones[this._iZone];
  
  int zone_real_id = zone_id * MAX_OBJ_PER_ZONE + this._iZoneObject;
  int obj_id = this.ZoneObject[zone_real_id];
  
  this._iZoneObject++;
  return this.Objects[obj_id];
}


void ZoneManager::ResetObjectIteratingZone() {
  this._iObjectIteratingZone = 0;
  this._IteratingZone = this._ActiveZones[4];
}

Mode7Object* ZoneManager::GetNextObjectIteratingZone()
{
  int current_zone = this._IteratingZone;
  
  if(this._iObjectIteratingZone > this.ZoneObjectCount[current_zone])
  {
    return null;
  }
  
  int zone_real_id = current_zone * MAX_OBJ_PER_ZONE + this._iObjectIteratingZone;
  int obj_id = this.ZoneObject[zone_real_id];
  
  this._iObjectIteratingZone++;
  return this.Objects[obj_id];
}


void ZoneManager::ResetObjectVicinityZones()
{
  this._iVicZone = 0;
  this._iVicZoneObject = 0;
}

Mode7Object* ZoneManager::GetNextObjectVicinityZones()
{
  if(this._iVicZone >= this._VicinityZoneCount) return null;
  
  int zone_id = this._VicinityZones[this._iVicZone];
  
  if(this._iVicZoneObject > this.ZoneObjectCount[zone_id])
  {
    this._iVicZone++;
    this._iVicZoneObject = 0;
  }
  
  if(this._iVicZone >= this._VicinityZoneCount) return null;
  zone_id = this._VicinityZones[this._iVicZone];
  
  int zone_real_id = zone_id * MAX_OBJ_PER_ZONE + this._iVicZoneObject;
  int obj_id = this.ZoneObject[zone_real_id];
  
  this._iVicZoneObject++;
  return this.Objects[obj_id];
}